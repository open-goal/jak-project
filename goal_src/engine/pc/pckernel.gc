;;-*-Lisp-*-
(in-package goal)

#|

  This file contains code that we need for the PC port of the game specifically.
  It should be included as part of the game engine package (engine.cgo).

  This file contains various types and functions to store PC-specific information
  and also to communicate between the game (GOAL) and the operating system.
  This way we can poll, change and display information about the system the game
  is running on, such as:
  - display devices and their settings, such as fullscreen, DPI, refresh rate, etc.
  - audio devices and their settings, such as audio latency, channel number, etc.
  - graphics devices and their settings, such as resolution, FPS, anisotropy, shaders, etc.
  - input devices and their settings, such as controllers, keyboards, mice, etc.
  - information about the game window (position, size)
  - PC-specific goodies, enhancements, fixes and settings.
  - whatever else.

  If you do not want to include these PC things, you should exclude it from the build system.

 |#


(#when PC_PORT


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;; updates
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;


(defmethod update-from-os pc-settings ((obj pc-settings))
  "Update settings from the PC kernel to GOAL."
  
  (set! (-> obj os) (pc-get-os))
  (pc-get-window-size (&-> obj win-width) (&-> obj win-height))
  (pc-get-window-scale (&-> obj dpi-x) (&-> obj dpi-y))
  
  (when (-> obj use-vis?)
    (if (= (-> *setting-control* default aspect-ratio) 'aspect4x3)
        (set! (-> obj aspect-ratio) ASPECT_4X3)
        (set! (-> obj aspect-ratio) ASPECT_16X9)
        )
    (set! (-> obj aspect-ratio-auto?) #f)
    )
  
  (let ((win-aspect (/ (the float (-> obj win-width)) (the float (-> obj win-height)))))
    (cond
      ((-> obj aspect-ratio-auto?)
       ;; the window determines the resolution
       (set! (-> obj aspect-ratio) win-aspect)
       (set! (-> obj width) (-> obj win-width))
       (set! (-> obj height) (-> obj win-height))
       )
      ((> win-aspect (-> obj aspect-ratio))
       ;; too wide
       (set! (-> obj width) (the int (* (-> obj aspect-ratio) (the float (-> obj win-height)))))
       (set! (-> obj height) (-> obj win-height))
       )
      ((< win-aspect (-> obj aspect-ratio))
       ;; too tall
       (set! (-> obj width) (-> obj win-width))
       (set! (-> obj height) (the int (/ (the float (-> obj win-width)) (-> obj aspect-ratio))))
       )
      (else
       ;; just right
       (set! (-> obj width) (-> obj win-width))
       (set! (-> obj height) (-> obj win-height))
       )
      )
    )
  

  (none))

(defmethod update-to-os pc-settings ((obj pc-settings))
  "Update settings from GOAL to the PC kernel."
  
  (cond
    ((-> obj letterbox?)
     (pc-set-letterbox (-> obj width) (-> obj height))
     )
    (else
     (pc-set-letterbox (-> obj win-width) (-> obj win-height))
     )
    )
  
  (none))

(defmethod update pc-settings ((obj pc-settings))
  "Update settings to/from PC kernel. Call this at the start of every frame.
   This will update things like the aspect-ratio, which will be used for graphics code later."

  (update-from-os obj)
  (update-to-os obj)

  (none))



(defmethod set-fullscreen! pc-settings ((obj pc-settings) (mode symbol))
  "toggles fullscreen mode"
  
  (if (= (-> obj fullscreen?) mode) ;; already fullscreen
    (return 0))
  
  (set! (-> obj fullscreen?) mode)
  (pc-set-fullscreen
          (cond ((= mode 'borderless) 2)
                (mode 1)
                (else 0)
                )
          0)
  0)

(defmethod set-size! pc-settings ((obj pc-settings) (width int) (height int))
  "toggles fullscreen mode"
  
  (pc-set-window-size width height)
  0)


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;; functions
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;


(when *debug-segment*

(define *pc-temp-string* (new 'debug 'string 1024 (the-as string #f)))

(defmethod draw pc-settings ((obj pc-settings) (buf dma-buffer))
  "debug draw"
  
  (clear *pc-temp-string*)
  (format *pc-temp-string* "game resolution: ~D x ~D~%" (-> obj width) (-> obj height))
  (format *pc-temp-string* "window size: ~D x ~D (~,,1f x ~,,1f)~%" (-> obj win-width) (-> obj win-height) (-> obj dpi-x) (-> obj dpi-y))
  (format *pc-temp-string* "target aspect: ~,,3f/~,,3f A: ~A/~A L: ~A~%" (-> obj aspect-ratio) (/ (the float (-> obj win-width)) (the float (-> obj win-height))) (-> obj aspect-ratio-auto?) (-> obj use-vis?) (-> obj letterbox?))
  (format *pc-temp-string* "fullscreen: ~A ~A~%" (-> obj fullscreen?) (-> obj vsync?))

  (draw-string-xy *pc-temp-string* buf 0 (- 240 (* 8 4)) (font-color default) (font-flags shadow kerning))
  
  (none))

)


(defmethod set-resolution pc-settings ((obj pc-settings) (height int))
  (set! (-> *video-parms* screen-sy) (/ height 2))
  (set! (-> *video-parms* relative-y-scale) (/ (the float height) 448.0))
  (set! (-> *math-camera* isometric vector 1 y) (* 0.5 (/ 448.0 height)))
  (set! (-> *math-camera* y-pix) (* 0.25 height))
  (set! (-> *math-camera* y-clip) (the float height))
  (set! (-> *video-parms* reset-video-mode) #t)
  (set! (-> *video-parms* screen-hy) (/ (-> *video-parms* screen-sy) 2))
  (set! (-> *video-parms* screen-miny) (- 2048 (-> *video-parms* screen-hy)))
  (set! (-> *video-parms* screen-maxy) (+ (-> *video-parms* screen-hy) 2048))
  (set! (-> *video-parms* screen-masky) (+ (-> *video-parms* screen-sy) -1))
  (set! (-> *pause-context* origin y) (the float (+ (-> *video-parms* screen-sy) -54)))
  (set! (-> *pause-context* height) (the float (-> *video-parms* screen-sy)))
  (set! (-> *font-default-matrix* vector 1 y) (-> *video-parms* relative-y-scale))
  (set! (-> *font-default-matrix* vector 3 y) (- (the float (-> *video-parms* screen-hy))))
  (set! (-> *video-parms* relative-y-scale-reciprical) 1.0)
  (set! *profile-y* (+ (-> *video-parms* screen-miny) 8))
  0
  )

(defmethod set-aspect pc-settings ((obj pc-settings) (aw float) (ah float))
  (let ((aspect (/ aw ah)))
    (set! (-> *video-parms* relative-x-scale) (/ ASPECT_4X3 aspect))
    (set! (-> *video-parms* relative-x-scale-reciprical) (/ aspect ASPECT_4X3))
    (set! (-> *font-default-matrix* vector 0 x) (-> *video-parms* relative-x-scale))
    (set! (-> *pc-settings* aspect-ratio) aspect)
    (set! (-> *pc-settings* aspect-ratio-auto?) #f)
    (set! (-> *pc-settings* use-vis?) #f)
    )
  0
  )



)



